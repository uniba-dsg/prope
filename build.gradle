apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "application"
apply plugin: "findbugs"

mainClassName = "prope.Prope"

repositories {
	jcenter()
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
	}
}

dependencies {
	compile 'net.lingala.zip4j:zip4j:1.3.+'
	compile 'org.apache.commons:commons-io:1.3.+'
	compile 'net.sf.saxon:Saxon-HE:9.6.+'
	compile files('./lib/bpp.jar')
	compile files('./lib/BPMNspector.jar')
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

findbugs {
	ignoreFailures = true
	effort = "max"
}

tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}

task deleteTmpDir(type: Delete){
	delete ('tmp')
}

run {
	if(project.hasProperty('args')){
		args project.args.split('\\s+')
	}
}

jar {
   from ( configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } ) {
   	    exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
   }
   manifest { attributes 'Main-Class': mainClassName }
}

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

